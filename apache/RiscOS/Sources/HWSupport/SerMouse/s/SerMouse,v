head	4.6;
access;
symbols
	SerMouse-0_10:4.6
	SerMouse-0_09:4.4
	RO_5_07:4.3
	SerMouse-0_08:4.3
	SerMouse-0_07:4.2
	SerMouse-0_06:4.1
	Ursula_merge:4.1
	mstphens_UrsulaRiscPCBuild_20Nov98:4.1
	Ursula_RiscPC:4.1.0.8
	rthornb_UrsulaBuild-19Aug1998:4.1
	UrsulaBuild_FinalSoftload:4.1
	rthornb_UrsulaBuild-12Aug1998:4.1
	aglover_UrsulaBuild-05Aug1998:4.1
	rthornb_UrsulaBuild-29Jul1998:4.1
	rthornb_UrsulaBuild-22Jul1998:4.1
	rthornb_UrsulaBuild-15Jul1998:4.1
	rthornb_UrsulaBuild-07Jul1998:4.1
	rthornb_UrsulaBuild-17Jun1998:4.1
	rthornb_UrsulaBuild-03Jun1998:4.1
	rthornb_UrsulaBuild-27May1998:4.1
	rthornb_UrsulaBuild-21May1998:4.1
	rthornb_UrsulaBuild_01May1998:4.1
	Spinner_B20_2:4.1.7.1
	Spinner_19_3:4.1.7.1
	Spinner_B18:4.1.7.1
	Spinner_B17:4.1.7.1
	Spinner_B15:4.1.7.1
	Spinner_B14:4.1.7.1
	Spinner_B13:4.1.7.1
	Spinner_B12:4.1.7.1
	Spinner_B10:4.1.7.1
	Daytona:4.1.0.6
	Daytona_bp:4.1
	Ursula:4.1.0.4
	Ursula_bp:4.1
	Spinner_B7:4.1.7.1
	RO_3_71:4.1.3.1
	ARTtmp_merge:4.1.7.1
	Spin_3Apr97:4.1.7.1
	ARTtmp:4.1.7.1.0.2
	RCA:4.1.0.2
	Spin_merge:4.1.7.1
	MergeFiles:4.1.3.1
	RO_3_70:4.1.3.1
	NC_1_06:4.1.7.1
	Spinner:4.1.7
	Spin_xx:4.1.5
	NC_xx:4.1.5.1
	RO_3_60:4.1.1.1
	StrongARM:4.1.3
	Black:4.1.1;
locks; strict;
comment	@# @;


4.6
date	2015.02.17.20.54.05;	author rsprowson;	state Exp;
branches;
next	4.5;
commitid	Swb3RPcfMqGf7pay;

4.5
date	2015.02.17.20.51.42;	author rsprowson;	state Exp;
branches;
next	4.4;
commitid	R0MVitS5Yk6g6pay;

4.4
date	2012.06.04.22.47.38;	author jlee;	state Exp;
branches;
next	4.3;
commitid	hua5hQDaRD3bvr7w;

4.3
date	2003.11.13.17.05.56;	author bavison;	state Exp;
branches;
next	4.2;

4.2
date	2001.03.16.16.03.45;	author sbrodie;	state Exp;
branches;
next	4.1;

4.1
date	96.11.05.09.37.36;	author nturton;	state Exp;
branches
	4.1.1.1
	4.1.3.1
	4.1.5.1
	4.1.7.1;
next	;

4.1.1.1
date	96.11.05.09.37.36;	author nturton;	state Exp;
branches;
next	;

4.1.3.1
date	96.11.06.01.42.58;	author nturton;	state Exp;
branches;
next	;

4.1.5.1
date	96.11.21.12.03.29;	author nturton;	state Exp;
branches;
next	;

4.1.7.1
date	96.11.29.20.54.50;	author nturton;	state Exp;
branches;
next	;


desc
@@


4.6
log
@Fix for null pointer dereference when no Serial device
While not addressing ticket #401 per se, we can at least not give the oflaofla error when no Serial device is found in DeviceFS.

Version 0.10. Tagged as 'SerMouse-0_10'
@
text
@; Copyright 1996 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
; > Sources.SerMouse

;---------------------------------------------------------------------------
; PointerRequest
;       In:     r0 = reason code (0 = request state)
;               r1 = device type (0 for us)
;       Out:    r2 = signed 32-bit X movement
;               r3 = signed 32-bit Y movement
;
;       Handle requests for mouse movements from the Kernel.
;
PointerRequest
        TEQ     r0, #PointerReason_Request              ; If not requesting then
        BNE     PointerIdentify                         ;   try identify.
        TEQ     r1, #PointerDevice_MicrosoftMouse       ; If not a serial mouse then
        TEQNE   r1, #PointerDevice_MSCMouse
        MOVNE   pc, lr                                  ;   pass on call.

        Push    "r10,r11"

        ADR     lr, MouseData                   ; Get DeltaX, DeltaY and Buttons.
        LDMIA   lr, {r2,r3,r10}

        MOV     lr, #0                          ; Zero DeltaX and DeltaY.
        STR     lr, DeltaX
        STR     lr, DeltaY

        LDRB    r11, LastButtons                ; Get button states on last poll.
        EORS    r11, r11, r10                   ; Get differences.
        Pull    "r10,r11,pc",EQ                 ; Claim vector if none.

        STRB    r10, LastButtons
        Push    "r0,r1,r9"

        TST     r11, #4                         ; Check left (SELECT).
        BEQ     %FT10

        TST     r10, #4
        MOVEQ   r0, #1
        MOVNE   r0, #2
        MOV     r1, #&70
        MOV     r9, #KEYV
        SWI     XOS_CallAVector
10
        TST     r11, #2                          ; Check centre (MENU).
        BEQ     %FT20

        TST     r10, #2
        MOVEQ   r0, #1
        MOVNE   r0, #2
        MOV     r1, #&71
        MOV     r9, #KEYV
        SWI     XOS_CallAVector
20
        TST     r11, #1                          ; Check right (ADJUST).
        Pull    "r0,r1,r9-r11,pc",EQ

        TST     r10, #1
        MOVEQ   r0, #1
        MOVNE   r0, #2
        MOV     r1, #&72
        MOV     r9, #KEYV
        SWI     XOS_CallAVector
        Pull    "r0,r1,r9-r11,pc"                ; Claim call.

;---------------------------------------------------------------------------
; PointerIdentify
;       In:     r0 = reason code 1
;               r1 = pointer to device type record (or 0)
;       Out:    r1 = pointer to updated list
;
;       Identify our pointer device.
;
PointerIdentify
        TEQ     r0, #PointerReason_Selected     ; If selected then
        BEQ     PointerSelected                 ;   initialise.
        TEQ     r0, #PointerReason_Identify     ; If not identify then
        MOVNE   pc, lr                          ;   pass on call.

        Entry   "r2"

        Debug   mod,"SM_PointerIdentify"

        ADR     r2, MSCData
        BL      AddPointerStruct
        EXIT    VS

        ADR     r2, MSData
        BL      AddPointerStruct
        EXIT

AddPointerStruct
; In:   r1 = pointer to device type record list or 0
;       r2 = pointer to device data
; Out:  r1 = pointer to extended list
        Entry   "r0-r4"

        LDRB    r4, [r2], #1                    ; r4=device type, r2->device name/token

  [ international
        MOV     r1, r2
        MOV     r2, #0
        BL      MsgTrans_Lookup                 ; r2->device name looked up
        EXIT    VS                              ; If lookup fails then don't add our record to list.
  ]
        MOV     r1, r2                          ; Save pointer to device name.
        BL      strlen                          ; r3=length of string pointed to by r2
        ADD     r3, r3, #MinPointerRecordSize   ; Includes byte for string terminator.
        MOV     r0, #ModHandReason_Claim        ; Claim space for a device type record.
        SWI     XOS_Module
        EXIT    VS

        LDR     r0, [sp, #4]                    ; Get back pointer to list we were passed.
        STR     r0, [r2, #PointerNext]          ; Tag it onto ours.
        MOV     r0, #0
        STR     r0, [r2, #PointerFlags]         ; No flags.
        STRB    r4, [r2, #PointerType]

        ADD     r0, r2, #PointerName
        BL      strcpy                          ; Copy name into record (r1 to r0).
        STR     r2, [sp, #4]                    ; Pass on updated record pointer.

        EXIT

strlen
; In:   r2->control char terminated string
; Out:  r3=length of string
        Entry   "r2"
        MOV     r3, #0
01
        LDRB    lr, [r2], #1
        CMP     lr, #" "
        ADDCS   r3, r3, #1
        BCS     %BT01
        EXIT

strcpy
; In:   r0->null terminated destination string
;       r1->control char terminated source string
        Entry   "r0,r1"
01
        LDRB    lr, [r1], #1
        CMP     lr, #" "
        STRCSB  lr, [r0], #1
        BCS     %BT01
        MOV     lr, #0
        STRB    lr, [r0]
        EXIT

MSCData
        DCB     PointerDevice_MSCMouse
 [ international
        DCB     "MSCName",0
 |
        DCB     "Mouse Systems Corp mouse",0
 ]

MSData
        DCB     PointerDevice_MicrosoftMouse
 [ international
        DCB     "MSName",0
 |
        DCB     "Microsoft mouse",0
 ]
        ALIGN

;---------------------------------------------------------------------------
; PointerSelected
;       In:     r1 = device type
;
;       Enable serial mouse if the type is one of ours, disable if not.
;
PointerSelected
        TEQ     r1, #PointerDevice_MicrosoftMouse       ; If not ours then
        TEQNE   r1, #PointerDevice_MSCMouse
        BNE     Disable                                 ;   make sure we're disabled.

        Entry

        Debug   mod,"SM_Enable"

        BL      MouseInit                               ; Initialise mouse.
        BL      Configure                               ; Configure serial.
        BL      Enable                                  ; Open serial, start interpreting.

        EXIT

;---------------------------------------------------------------------------
; MouseInit
;       r1 = mouse type
;
;       Initialise serial mouse.
;
MouseInit
        Entry

        Debug   mod,"SM_MouseInit"

        MOV     lr, #0                          ; Initialise mouse data.
        STR     lr, DeltaX
        STR     lr, DeltaY
        STR     lr, Buttons
        ADR     lr, MouseIdle
        STR     lr, State

        EXIT

;---------------------------------------------------------------------------
; Enable
;
;       Ensure that the serial input stream is open.
;
Enable
        Entry   "r0-r2"

        LDR     r1, SerialInHandle
        TEQ     r1, #0                          ; If serial already open then
        EXIT    NE                              ;   exit.

        Debug   mod,"SM_Enable"

        MOV     r0, #0                          ; Not locked yet.
        STRB    r0, Locked

        BL      OpenSerial

        MOVVC   r0, #TickerV                    ; Start interpreting serial input.
        ADRVC   r1, TickerVHandler
        MOVVC   r2, r12
        SWIVC   XOS_Claim

        STRVS   r0, [sp]

        EXIT

;---------------------------------------------------------------------------
; OpenSerial
;
;       Ensure that the serial input stream is open.
;
OpenSerial
        Entry   "r0-r2", 64

        MOV     r2, sp
        ADR     r1, SerialInFilename
10
        LDRB    r0, [r1], #1
        CMP     r0, #0
        STRNEB  r0, [r2], #1
        BNE     %BT10        

        MOV     r0, #SerialOp_GetDeviceName
        SWI     XOS_SerialOp
        ; r1 should now be correct device name or preserved on error
        ; i.e. left pointing at our default name
        ADD     lr, sp, #63
20
        SUBS    r0, r1, lr                      ; Basic buffer overflow check
        LDRNEB  r0, [r1], #1
        CMPNE   r0, #0
        STRB    r0, [r2], #1
        BNE     %BT20

        MOV     r0, #open_read :OR: open_mustopen       ; Open serial device for input.
        MOV     r1, sp
        SWI     XOS_Find
        STRVS   r0, [sp, #Proc_RegOffset]
        MOVVS   r0, #0
        STR     r0, SerialInHandle              ; Store handle, mark serial as open.

        EXIT

SerialInFilename
        DCB     "Devices:$.", 0, "Serial",0
        ALIGN

;---------------------------------------------------------------------------
; Disable
;
;       Close the serial input stream if it's open.
;
Disable
        EntryS  "r0-r2"

        Debug   mod,"SM_Disable"

        MOV     r0, #TickerV
        ADR     r1, TickerVHandler
        MOV     r2, r12
        SWI     XOS_Release

        LDR     r1, SerialInHandle
        TEQ     r1, #0
        MOVNE   r0, #OSFind_Close
        STRNE   r0, SerialInHandle
        SWINE   XOS_Find

        EXITS

;---------------------------------------------------------------------------
; Configure
;
;       Configure serial.
;
Configure
        Entry   "r0-r2"

        Debug   mod,"SM_Configure"

        MOV     r0, #SerialOp_Status            ; Set up serial status.
        MOV     r1, #&00000036                  ; RTS high, ignore ~CTS, DTR high, ignore ~DSR, ignore ~DCD
        MOV     r2, #&FFFFFF00
        SWI     XOS_SerialOp
        BVS     %FT99

        MOV     r0, #SerialOp_DataFormat        ; Set up data format.
        MOV     r1, #&00000000                  ; 8bit word, 1 stop bit, no parity.
        SWI     XOS_SerialOp
        BVS     %FT99

        MOV     r0, #SerialOp_RXBaud            ; Set input baud rate
        MOV     r1, #4                          ; to 1200bits/sec.
        SWI     XOS_SerialOp
99
        STRVS   r0, [sp]
        EXIT

        END
@


4.5
log
@Use some symbolic names instead of numbers
Binary identical, not tested.
@
text
@d281 1
@


4.4
log
@Make use of OS_SerialOp 10 when opening serial stream
Detail:
  s/SerMouse - Use OS_SerialOp 10 when opening serial stream, to make sure we cope with serial devices which aren't called "Serial"
  s/GetAll - GET of Hdr:SerialOp
Admin:
  Module builds and runs, but funcitonality untested due to lack of serial mouse


Version 0.09. Tagged as 'SerMouse-0_09'
@
text
@d307 1
a307 1
        MOVNE   r0, #0
@


4.3
log
@Commit of Kevin's 32-bit SerialMouse.

Version 0.08. Tagged as 'SerMouse-0_08'
@
text
@d256 21
a276 1
        Entry   "r0,r1"
d279 1
a279 1
        ADR     r1, SerialInFilename
d287 1
a287 1
        DCB     "Devices:$.Serial",0
d323 1
a323 1
        MOV     r0, #0                          ; Set up serial status.
d329 1
a329 1
        MOV     r0, #1                          ; Set up data format.
d334 1
a334 1
        MOV     r0, #5                          ; Set input baud rate
@


4.2
log
@  Updated build structure to use the shared AAsmModule makefile.
  Updated to build using objasm instead of aasm.
  Sources changed to be objasm-compatible.
Admin:
  Requires Library 0.71 or later.
  Requires BuildSys 3.06 or later.
  Requires Env 0.65 or later.

Version 0.07. Tagged as 'SerMouse-0_07'
@
text
@d31 1
a31 1
        MOVNES  pc, lr                                  ;   pass on call.
d92 1
a92 1
        MOVNES  pc, lr                          ;   pass on call.
d100 1
a100 1
        EXITS   VS
d104 1
a104 1
        EXITS
d200 1
a200 1
        EXITS
d246 2
d276 1
a276 1
        Entry   "r0-r2"
d307 1
a307 1
        EXITS   VS
d312 1
a312 1
        EXITS   VS
d317 2
a318 1

@


4.1
log
@Initial revision
@
text
@d94 1
a94 1
        ENTRY   "r2"
d110 1
a110 1
        ENTRY   "r0-r4"
d142 1
a142 1
        ENTRY   "r2"
d154 1
a154 1
        ENTRY   "r0,r1"
d192 1
a192 1
        ENTRY
d209 1
a209 1
        ENTRY
d228 1
a228 1
        ENTRY   "r0-r2"
d254 1
a254 1
        ENTRY   "r0,r1"
d274 1
a274 1
        ENTRY   "r0-r2"
d297 1
a297 1
        ENTRY   "r0-r2"
@


4.1.7.1
log
@NCOS 1.06 Imported from Zip drive
@
text
@@


4.1.5.1
log
@Import from SrcFiler
@
text
@@


4.1.3.1
log
@Import from cleaned 370 CD
@
text
@@


4.1.1.1
log
@Import from cleaned 360 CD
@
text
@@

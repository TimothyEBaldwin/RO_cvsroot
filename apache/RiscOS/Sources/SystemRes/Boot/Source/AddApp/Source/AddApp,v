head	4.2;
access;
symbols
	Boot-1_26:4.2
	Boot-1_25:4.2
	Boot-1_24:4.2
	Boot-1_23:4.2
	Boot-1_22:4.2
	Boot-1_21:4.2
	Boot-1_20:4.2
	Boot-1_19:4.2
	Boot-1_18:4.2
	Boot-1_17:4.2
	Boot-1_16:4.2
	Boot-1_15:4.2
	Boot-1_14:4.2
	Boot-1_13:4.2
	Boot-1_12:4.2
	Boot-1_11:4.2
	Boot-1_10:4.2
	Boot-1_09:4.2
	Boot-1_08:4.2
	Boot-1_07:4.2
	Boot-1_06:4.2
	Boot-1_05:4.2
	Boot-1_04:4.2
	Boot-1_03:4.2
	Boot-1_02:4.2
	Boot-1_01:4.2
	Boot-1_00:4.2
	Boot-0_99:4.2
	Boot-0_98:4.2
	Boot-0_97:4.2
	Boot-0_96:4.2
	Boot-0_95:4.2
	Boot-0_94:4.2
	Boot-0_93:4.2
	Boot-0_92:4.2
	Boot-0_91:4.2
	Boot-0_90:4.2
	Boot-0_89:4.2
	Boot-0_88:4.2
	Boot-0_87:4.2
	Boot-0_86:4.2
	Boot-0_85:4.2
	Boot-0_84:4.2
	Boot-0_83:4.2
	Boot-0_82:4.2
	Boot-0_81:4.2
	Boot-0_80:4.2
	Boot-0_79:4.2
	Boot-0_78:4.2
	Boot-0_77:4.2
	Boot-0_76:4.2
	Boot-0_75:4.2
	Boot-0_74:4.2
	Boot-0_73:4.2
	Boot-0_72:4.2
	Boot-0_71:4.1.1.1
	Boot-0_70:4.1.1.1
	Boot-0_69:4.1.1.1
	Boot-0_68:4.1.1.1
	Boot-0_67:4.1.1.1
	nbingham_boot_991007:4.1.1.1
	mstphens_UrsulaRiscPCBuild_20Nov98:4.1.1.1
	Ursula_RiscPC:4.1.1.1.0.8
	rthornb_UrsulaBuild-19Aug1998:4.1.1.1
	UrsulaBuild_FinalSoftload:4.1.1.1
	rthornb_UrsulaBuild-12Aug1998:4.1.1.1
	aglover_UrsulaBuild-05Aug1998:4.1.1.1
	rthornb_UrsulaBuild-29Jul1998:4.1.1.1
	rthornb_UrsulaBuild-22Jul1998:4.1.1.1
	rthornb_UrsulaBuild-15Jul1998:4.1.1.1
	rthornb_UrsulaBuild-07Jul1998:4.1.1.1
	rthornb_UrsulaBuild-17Jun1998:4.1.1.1
	rthornb_UrsulaBuild-03Jun1998:4.1.1.1
	rthornb_UrsulaBuild-27May1998:4.1.1.1
	rthornb_UrsulaBuild-21May1998:4.1.1.1
	rthornb_UrsulaBuild_01May1998:4.1.1.1
	Daytona:4.1.1.1.0.6
	Daytona_bp:4.1.1.1
	Ursula:4.1.1.1.0.4
	Ursula_bp:4.1.1.1
	RO_3_71:4.1.3.1
	ART_bp:4.1.1.1
	ART:4.1.1.1.0.2
	MergeFiles:4.1.3.1
	RO_3_70:4.1.3.1
	RO_3_60:4.1.1.1
	StrongARM:4.1.3
	Black:4.1.1;
locks; strict;
comment	@# @;


4.2
date	2003.02.13.17.22.31;	author rsprowson;	state Exp;
branches;
next	4.1;

4.1
date	96.11.05.09.51.59;	author nturton;	state Exp;
branches
	4.1.1.1
	4.1.3.1;
next	;

4.1.1.1
date	96.11.05.09.51.59;	author nturton;	state Exp;
branches;
next	;

4.1.3.1
date	96.11.06.03.39.30;	author nturton;	state Exp;
branches;
next	;


desc
@@


4.2
log
@Finally got round to storing this stuff.
Many changes,highlights being:
 freepool now creates its 'o' directory in the top Makefile
 safelogin tweaked so it can find its header files again
 stopped EXITS macro moaning about no ENTRYS in Appsize and AddApp
 added 2DTools to universal boot sequence
 Added RO370/400/500Hook though the 370 and 400 are just empty at present
 newer vprotect added from Alan Glover
 add Utils.NetChoices
 delete ~CDReinit binary,shuffle sources a bit
 misc changes to obey files from Steve
The Makefile now expects a TARGET to be defined.Currently the 500 one is
the only one tested,though UNIVERSAL is also defined and outputs a boot
sequence like you would have got with !MkInstall in V0.71.
This picks up files which might differ between boots,and also adds
extra files which change when the UserIF changes.

Version 0.72. Tagged as 'Boot-0_72'
@
text
@        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:Proc
        GET     Hdr:ModHand
        GET     Hdr:ResourceFS

        GBLL    Debug
Debug   SETL    {FALSE}

        GBLS    GetAroundBleedingAAsmYetAgain
 [ Debug
GetAroundBleedingAAsmYetAgain SETS " GET Hdr:Debug"
 |
GetAroundBleedingAAsmYetAgain SETS "; No debug required"
 ]
$GetAroundBleedingAAsmYetAgain

 [ Debug
Host_Debug SETL {FALSE}
Debug_MaybeIRQ SETL {FALSE}
 ]

        Entry

        MOV     r0, r12
        MOV     r2, r13
        BL      strcpy_advance
        MOV     r6, r0
        MOV     r2, r0
        B       %FT20

10
        LDRB    lr, [r6, #-1]!
        TEQ     lr, #'.'
        BEQ     %FT30
20
        CMP     r6, r12
        BHI     %BT10

        ADR     r1, CSD_name
        B       %FT40

30
        MOV     lr, #0
        STRB    lr, [r6], #1
        MOV     r1, r12

40
        ; r1->directory name
        ; r2->past wildcard
        ; r6->wildcard
        ADD     r2, r2, #1+3
        BIC     r2, r2, #3
        MOV     r0, #10
        MOV     r4, #0

50
        MOV     r3, #1
        SUB     r5, r13, r2
 [ Debug :LAND: {FALSE}
        DSTRING r1,"Dir:",cc
        DSTRING r6," wild:"
 ]
        SWI     OS_GBPB
 [ Debug :LAND: {FALSE}
        DREG    r3, "Num found:",cc
        DREG    r4, " continue:"
 ]
        TEQ     r3, #1
        BNE     %FT60

        ; Put a sensible access into the block
        MOV     lr, #3
        STR     lr, [r2, #12]

        ; Add app to ResourceFS
        BL      AddAppToResourceFS

60
        CMP     r4, #-1
        BNE     %BT50

        CLRV
        EXIT

CSD_name        DCB     "@@",0
        ALIGN

ResourceOverhead * 3*(20+5+7+3+4+1+7+3) + 4
; to full filename + Apps. + .!Help\0 + padding + file size + / + .!Help\n + padding
; + end indicator

;
; In
;  r1->directory name
;  r2->file info block (as per OS_GBPB 10)
;
BootPresent * 1
BootIsIconsprites * 2
HelpPresent * 4

AddAppToResourceFS Entry "r0,r2,r3,r4,r5,r8"
        BL      FaffMakeFlags
 [ Debug
        DREG    r8, "App flags:"
 ]

        MOV     r0, #0
        MOV     r3, r1
        MOV     r4, r2
        MOV     r2, #0
        BL      StuffBigBlock

        MOV     r3, r0
        MOV     r0, #ModHandReason_Claim
 [ Debug
        DREG    r3, "BigBlock size "
 ]
        SWI     OS_Module

        MOV     r0, r2
        ADD     r2, r0, r3              ; set up the block limit
        MOV     r3, r1
        MOV     r5, r0                  ; Save the block start
        BL      StuffBigBlock

        MOV     r0, r5
 [ Debug
        DREG    r0, "ResourceFS_RegisterFiles "
 |
        SWI     ResourceFS_RegisterFiles
 ]

        EXIT

DotPlingBoot    DCB     ".!Boot",0
DotPlingHelp    DCB     ".!Help",0
DotPlingRun     DCB     ".!Run",0
Slash           DCB     "/",0
FilerRun        DCB     "Filer_Run ",0
DotPlingSprites DCB     ".!Sprites",0
Iconsprites     DCB     "Iconsprites ",0
        ALIGN
; In
;  r1->directory name
;  r2->file info block (as per OS_GBPB 10)
; Out:
;  r8=flags

FaffMakeFlags Entry "r0,r1,r2,r3,r4,r5,r6,r7"
        ADD     r0, r2, #20
        MOV     r3, r0
        BL      strlen_accumulate       ; r3->end of file name
        ADD     r0, r3, #1
        MOV     r4, r2
        MOV     r2, r13
        BL      strcpy_advance
        MOV     r1, #'.'
        BL      stuffbyte
        ADD     r1, r4, #20
        BL      strcpy_advance
        ADD     r6, r3, #1              ; Start of name
        MOV     r7, r0                  ; end of name
        ADR     r1, DotPlingBoot
        BL      strcpy_advance
        MOV     r0, #17
        MOV     r1, r6
 [ Debug
        DSTRING r1, "Check for "
 ]
        SWI     OS_File
        TEQ     r0, #0
        MOVNE   r8, #BootPresent
        BNE     %FT20

        MOV     r0, r7
        ADR     r1, DotPlingSprites
        MOV     r2, r13
        BL      strcpy_advance
        MOV     r0, #17
        MOV     r1, r6
 [ Debug
        DSTRING r1, "Check for "
 ]
        SWI     OS_File
        TEQ     r0, #0
        MOVNE   r8, #BootPresent :OR: BootIsIconsprites
        MOVEQ   r8, #0

20
        ; boot sorted out
        MOV     r0, r7
        ADRL    r1, DotPlingHelp
        MOV     r2, r13
        BL      strcpy_advance
        MOV     r0, #17
        MOV     r1, r6
 [ Debug
        DSTRING r1, "Check for "
 ]
        SWI     OS_File
        TEQ     r0, #0
        ORRNE   r8, r8, #HelpPresent

        EXIT

;
; In:
;  r0->big block
;  r2->big block limit
;  r3->directory name
;  r4->info block
;  r8=flags
; Out:
;  big block stuffed and r0 advanced
;

StuffBigBlock Entry "r1,r5,r6,r7"

        TST     r8, #BootPresent
        BEQ     %FT10

        ADR     r6, DotPlingBoot
        TST     r8, #BootIsIconsprites
        ADREQ   r5, Slash
        ADREQ   r7, DotPlingBoot
        ADRNE   r5, Iconsprites
        ADRNEL  r7, DotPlingSprites
        BL      StuffFileToBigBlock

10
        TST     r8, #HelpPresent
        ADRNEL  r5, FilerRun
        ADRNEL  r6, DotPlingHelp
        ADRNEL  r7, DotPlingHelp
        BLNE    StuffFileToBigBlock

        ADR     r5, Slash
        ADRL    r6, DotPlingRun
        ADRL    r7, DotPlingRun
        BL      StuffFileToBigBlock

        MOV     r1, #0
        BL      stuffword

        EXIT


AppsDot         DCB     "Apps.",0
        ALIGN

;
; In
;  r0->destination
;  r2->limit
;  r3->directory name
;  r4->info block
;  r5->command prefix
;  r6->.!Boot or .!Run or .!Help as appropriate for filename
;  r7->.!Sprites, .!Boot etc as appropriate for file contents
; Out
;  r0->end rounded up
;
StuffFileToBigBlock Entry "r1,r8,r9,r10"

        MOV     r8, r0                  ; r8->block start
        BL      stuffword               ; link to next file
        LDR     r1, [r4, #0]
        MOV     r1, r1, ASL #24
        LDR     lr, =&ffffeb00          ; obey file type
        ORR     r1, lr, r1, LSR #24
        BL      stuffword               ; load
        LDR     r1, [r4, #4]
        BL      stuffword               ; exec
        BL      stuffword               ; 'length'
        LDR     r1, [r4, #12]
        BL      stuffword               ; attributes
        ADR     r1, AppsDot
        BL      strcpy_advance          ; Apps.
        ADD     r1, r4, #20
        BL      strcpy_advance          ; <App>
        MOV     r1, r6
        BL      strcpy_advance          ; .<thing>
        ADD     r0, r0, #1+3+4
        BIC     r0, r0, #3
        MOV     r9, r0                  ; r9->file start
        MOV     r1, r5
        BL      strcpy_advance          ; / or iconsprites
        MOV     r1, r3
        BL      strcpy_advance          ; <dir>
        MOV     r1, #'.'
        BL      stuffbyte               ; .
        ADD     r1, r4, #20
        BL      strcpy_advance          ; <app>
        MOV     r1, r7
        BL      strcpy_advance          ; .<thing>
        MOV     r1, #10
        BL      stuffbyte               ; <nl>
        SUB     r1, r0, r9
        ADD     r10, r0, #3
        BIC     r10, r10, #3            ; r10->next block start
        ADD     r0, r8, #12
        BL      stuffword               ; file length
        ADD     r1, r1, #4
        SUB     r0, r9, #4
        BL      stuffword               ; file length+4
        SUB     r1, r10, r8
        MOV     r0, r8
        BL      stuffword               ; link to next file

        MOV     r0, r10
        EXIT


;
; copy r1 to r0, limit is r2
;
strcpy_advance Entry "r1,r3"
        MOV     r3, r1
10      LDRB    r1, [r3], #1
        CMP     r1, #31
        MOVLSS  r1, #0
        BL      stuffbyte
        BNE     %BT10
        SUB     r0, r0, #1
        EXIT

;
; word in r1 stuffed to [r0]
; stuff limit is r2
;
stuffword Entry "r1"
        BL      stuffbyte
        MOV     r1, r1, LSR #8
        BL      stuffbyte
        MOV     r1, r1, LSR #8
        BL      stuffbyte
        MOV     r1, r1, LSR #8
        BL      stuffbyte
        EXIT

;
; byte in r1 stuffed to [r0]
; r2 is stuff limit register
;
stuffbyte
        CMP     r0, r2
        STRLOB  r1, [r0], #1
        ADDHS   r0, r0, #1
        MOVS    pc, lr

strlen_accumulate Entry "r0"
10
        LDRB    lr, [r0], #1
        TEQ     lr, #0
        ADDNE   r3, r3, #1
        BNE     %BT10
        EXIT

 [ Debug
        InsertDebugRoutines
 ]

        DCB     "AddApp 1.00 29-Nov-1993  © Acorn Computers Ltd 1993", 0
        ALIGN

        END
@


4.1
log
@Initial revision
@
text
@d84 2
a85 1
        EXITS
@


4.1.3.1
log
@Import from cleaned 370 CD
@
text
@@


4.1.1.1
log
@Import from cleaned 360 CD
@
text
@@

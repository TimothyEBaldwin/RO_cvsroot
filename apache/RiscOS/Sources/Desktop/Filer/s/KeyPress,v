head	4.5;
access;
symbols
	Filer-2_43:4.5
	Filer-2_42:4.5
	Filer-2_41:4.5
	Filer-2_40:4.4
	Filer-2_39:4.4
	Filer-2_38:4.4
	Filer-2_37:4.4
	Filer-2_36:4.4
	Filer-2_35:4.4
	Filer-2_34:4.4
	Filer-2_33:4.4
	Filer-2_32:4.3
	Filer-2_31:4.2
	Filer-2_30:4.1;
locks; strict;
comment	@# @;


4.5
date	2015.08.01.19.30.12;	author jlee;	state Exp;
branches;
next	4.4;
commitid	wlEeHupJlGcClBvy;

4.4
date	2012.09.25.07.46.58;	author rsprowson;	state Exp;
branches;
next	4.3;
commitid	ervohtjnPZaYSSlw;

4.3
date	2012.09.12.23.33.24;	author srevill;	state Exp;
branches;
next	4.2;
commitid	wsLbqxWTIbIAxikw;

4.2
date	2012.09.11.21.45.52;	author rsprowson;	state Exp;
branches;
next	4.1;
commitid	HuW3S2AXkdHGY9kw;

4.1
date	2012.05.07.21.33.45;	author bavison;	state Exp;
branches;
next	;
commitid	ucGU0rswOADBZP3w;


desc
@@


4.5
log
@Fix null pointer dereferences in keyboard shortcut handling. Fix Ctrl-R renaming not always working.
Detail:
  s/Redraw - Introduce always_create_rename_writeable, as a variation of create_rename_writeable. This new version ignores the mouse position and positions the caret at the end of the filename.
  s/KeyPress - Fix delete shortcut handling to do nothing if nothing selected (avoids null pointer dereference in Filer_Action). Fix null pointer dereference in 'open parent' shortcut handling if parent already open. Change rename handling to use always_create_rename_writeable instead of trying to generate a fake mouse position (old code only worked for some filer view modes). Fix comments for next/prev dir shortcuts to match reality, and wrap rename code in AltRenaming switch to match the implementation in s/Redraw.
Admin:
  Tested on Pandaboard
  Fixes null pointer dereferences reported on forums:
  https://www.riscosopen.org/forum/forums/4/topics/3447


Version 2.41. Tagged as 'Filer-2_41'
@
text
@; Copyright 2012 Castle Technology Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
;++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; event_key_pressed

; In    r1 -> wimp_eventstr
;       [r1, #0]  = window handle
;       [r1, #4]  = icon handle
;       [r1, #8]  = x offset of caret
;       [r1, #12] = y offset of caret
;       [r1, #16] = caret height
;       [r1, #20] = index of caret into string
;       [r1, #24] = key pressed
; Out   much corruption
event_key_pressed Entry

        ; Check if in window we're interested in
        LDR     r3, [r1, #0]
        LDR     r14, h_menuwindow_loc
        TEQ     r14, #0
        LDRNE   r14, [r14]
        CMP     r14, r3
        LDRNE   r5, renaming_chunkaddr
        BNE     %FT06

        ; Pass on if not a Carriage Return (in a writeable icon)
        LDR     r0, [r1, #24]
        MOV     r2, #0          ; No buttons were pressed
        STR     r2, buttonstate ; for reopening menus
        MOV     r4, #0          ; Icon 0 was 'clicked'
        CMP     r0, #CR
        BNE     %FT10
        B       click_menuwindow

06      ; If not the menu window, then is there a writeable icon?
        LDR     r0, [r5,#re_windowhandle] ; if windowhandle is zero,
        CMP     r0, #0                    ; there is currently no writeable icon
        BEQ     %FT30

        ; it a return in the renaming writeable icon?
        LDR     r0, [r1, #24]
        CMP     r0, #CR
        BEQ     %FT07

        ; No? What about an ESCAPE?
        CMP     r0, #&1b
        BNE     %FT10

; ESCAPE pressed in writeable icon

        [ AltRenaming
        BL      remove_rename_writeable
        ]

        B       %FT20

; RETURN pressed in writeable icon

07
        Push    "r1"
        MOV     r0, #-1
        SWI     Wimp_SetCaretPosition       ; Remove the caret

        ; What dirviewer block and file block was the renamed file?
        LDR     r1, renaming_chunkaddr
        ADD     r1, r1, #re_dirblock
        LDR     r4, [r1]                  ; dirviewer
        LDR     r5, [r1, #4]              ; fileblock

        STR     r4, sel_dir               ; r4 -> dirblock
        STR     r5, sel_fileblock         ; r5 -> fileblock

        Push    "r14"
        LDR     r2, renaming_chunkaddr    ; copy new filename
        ADD     r2, r2, #re_textstring
        ADR     r1, ms_writeable_leafname
        BL      strcpy
        Pull    "r14"

        LDR     r2, [r4, #d_dirnamestore] ; r2 -> dirname
        STR     r2, sel_dirname

        LDR     r2, [r4, #d_filenames]
        LDR     r3, [r5, #df_fileptr]
 [ not_16bit_offsets
        ADD     r2, r2, r3                ; r2 -> leafname
 |
        ADD     r2, r2, r3, LSR #16       ; r2 -> leafname
 ]
        STR     r2, sel_leafname

        BL      strcmp_sensitive          ; has the filename been altered?
        BNE     %FT08                     ; yep, then skip
        BL      remove_rename_writeable   ; else erase the icon
        B       %FT09                     ; don't perform rename
08
        Push    "r15"                     ; Push pc onto stack for return
        BL      DecodeMenu_File_Rename    ; Carry out rename
        NOP                               ; pre-StrongARM pushed R15+12
09
        Pull    "r1"

10      ; Pass the key on
        LDR     r0, [r1, #24]
15      SWI     XWimp_ProcessKey
20
        EXIT

30	; locate the directory

        LDR     r0, [r1, #0]
        BL      FindDir
        LDR     r0, [r1, #24]
        BNE     %BT15

        ; Set up selection for those operations that need it
        ;  (harmless for others)
        BL      InitSelection

        TEQ     r0, #CR                   ; Enter (Ctrl-M)
        BEQ     key_open
        TEQ     r0, #8                    ; Backspace (Ctrl-H)
        BEQ     key_open_parent
        TEQ     r0, #1                    ; Ctrl-A
        BEQ     DecodeMenu_SelectAll
        TEQ     r0, #6                    ; Ctrl-F
        BEQ     key_toggle_format
 [ AltRenaming
        TEQ     r0, #18                   ; Ctrl-R
        BEQ     key_rename
 ]
        TEQ     r0, #19                   ; Ctrl-S
        BEQ     key_toggle_sort
 [ AddSetDirectory
        TEQ     r0, #23                   ; Ctrl-W
        BEQ     DecodeMenu_SetDir
 ]
        TEQ     r0, #26                   ; Ctrl-Z
        TEQNE   r0, #27                   ; Escape
        BEQ     DecodeMenu_ClearSel

        TEQ     r0, #11                   ; Ctrl-K
        BEQ     key_delete_files

        SUB     r2, r0, #&180
        TEQ     r2, #&A                   ; TAB
        BEQ     key_next_dir
        TEQ     r2, #&1A                  ; Shift-TAB
        BEQ     key_prev_dir
        TEQ     r2, #&22                  ; Ctrl-F2
        BEQ     key_close_dir

        ; Pass on
        B       %BT15

key_open
        ; TODO
        EXIT

key_delete_files
        CMP     r5, #Nowt
        EXIT    EQ
        Push    "r15"
        BL      DecodeMenu_File_Delete
        NOP
        EXIT

key_toggle_sort
        LDRB    r0, [r4, #d_viewmode]
        MOV     r1, #1
        ADD     r0, r1, r0, LSR #dbb_sortmode
        AND     r0, r0, #db_sortmode :SHR: dbb_sortmode
        ADD     r0, r0, #mo_display_sortname
        B       DecodeMenu_Display

key_toggle_format
        LDRB    r0, [r4, #d_viewmode]
        MOV     r1, #1
        ASSERT  dbb_displaymode = 0
        ADD     r0, r1, r0
        AND     r0, r0, #db_displaymode :SHR: dbb_displaymode
        CMP	r0, #3
	MOVHS	r0, #0
        B       DecodeMenu_Display

 [ AltRenaming
key_rename
	LDR	r0, [r1, #0]
        CMP     r5, #Nowt
	EXIT	EQ

	; need bounding box for icon creation
	; r3  = selected file number
	; r4 -> selected dirviewer block
	; r5 -> selected fileinfo block

	STR	r0, windowhandle
	BL	GetFileBox

        BL      always_create_rename_writeable
        EXIT
 ]

key_open_parent
        Push    "r15"
        BL      DecodeMenu_OpenParent
        NOP
        EXIT    VS
        TEQ     r7, #0
        LDRNE   r0, [r7, #d_handle]
        BLNE    AssignFocus
        EXIT

key_close_dir ROUT  ; NOENTRY
        LDR     r1, [r4, #d_link]
        CMP     r1, #Nowt
        LDREQ   r1, ViewerList

        LDR     r0, [r4, #d_handle]
        CMP     r1, r4
        BEQ     %FT10

        ;close and pass focus to window

        BL      close_window_r0
        BLVS    LocalReportError
        LDR     r0, [r1, #d_handle]
        BL      AssignFocus
        EXIT
10      BL      close_window_r0
        EXIT

; pass input focus to the next dir in the list

key_next_dir
        LDR     r1, [r4, #d_link]
        CMP     r1, #Nowt
        LDREQ   r1, ViewerList
        LDR     r0, [r1, #d_handle]
        CMP     r1, r4
        EXIT    EQ
;        BL      AssignFocus
        BL      front
        EXIT

; pass input focus to the previous dir in the list

key_prev_dir ROUT       ; NOENTRY

        ; run down the list until we find a link to the current
        ;   viewer or we hit the end in which case we were first

        LDR     r1, ViewerList
        LDR     r0, [r1, #d_link]
10      CMP     r0, #Nowt
        CMPNE   r0, r4
        MOVNE   r1, r0
        LDRNE   r0, [r0, #d_link]
        BNE     %BT10

        LDR     r0, [r1, #d_handle]
        CMP     r1, r4
        EXIT    EQ
;        BL      AssignFocus
        BL      front
        EXIT

; Assign input focus to the given window
;
; In    r0 = window handle

AssignFocus
        Push    "r0-r5,lr"
        MOV     r1,#-1
        MOV     r2,#0
        MOV     r3,#0
        MOV     r4,#1<<25
        MOV     r5,#-1
        SWI     XWimp_SetCaretPosition
        Pull    "r0-r5,pc"

        END
@


4.4
log
@Adjust shortcut key
Turns out !Draw and !Edit (from which the delete precedent was taken) were themselves not the best example, this change uses the approved ^K key combo.
German messages updated to include shortcut menu hints.

Version 2.33. Tagged as 'Filer-2_33'
@
text
@d141 1
d144 1
d159 1
a159 1
        TEQ     r2, #&A                   ; Shift-TAB
d161 1
a161 1
        TEQ     r2, #&1A                  ; Ctrl-TAB
d174 2
d199 1
d213 1
a213 7
	; need co-ordinates for positioning caret
	LDR	r0, filebox1 + 8
	LDR	r1, filebox1 + 12
	STR	r0, relmousex
	STR	r1, relmousey

        BL      create_rename_writeable
d215 1
d221 4
a224 2
        LDRVC   r0, [r7, #d_handle]
        BLVC    AssignFocus
@


4.3
log
@Tweak to positioning of input focus.

Now places input focus into any filer window that is opened or brought
to the front. Previously, the window you opened from, usually behind
the new window, would be the one with focus.

Version 2.32. Tagged as 'Filer-2_32'
@
text
@d153 1
a153 1
        TEQ     r0, #24
@


4.2
log
@Swap hot key for something less fat finger prone
When used with 'confirm deletes' off (which is probably a common mode of operation so as to not have to confirm every single file) it's now all too easy to delete whole directories of files if selected.
Shortcut key changed to CTRL-X (to match the build in apps) and pave the way for a future with CTRL-C and CTRL-V ala Windows.

Version 2.31. Tagged as 'Filer-2_31'
@
text
@d253 1
a253 1
        BL      AssignFocus
d275 1
a275 1
        BL      AssignFocus
@


4.1
log
@  Keyboard shortcuts in the Filer
Detail:
  Tab - focus to next window
  Shift-Tab - focus to previous window
  Backspace - open parent
  ^W - Set working directory
  ^F - Change display format
  ^S - Change sort order
  ^F2 - Close current window and move focus
  ^A - Select all
  ^Z/Esc - clear selection
  Del - delete selected object(s)  (WARNING - No confirmation)
  Also installed error handler to help prevent it becoming the innocent victim
  of failures in callback handlers etc.
Admin:
  Changes received from Adrian Lees

Version 2.30. Tagged as 'Filer-2_30'
@
text
@d133 1
a133 1
        TEQ     r0, #CR
d135 1
a135 1
        TEQ     r0, #8
d137 1
a137 1
        TEQ     r0, #1
d139 1
a139 1
        TEQ     r0, #6
d141 1
a141 1
        TEQ     r0, #18
d143 1
a143 1
        TEQ     r0, #19
d146 1
a146 1
        TEQ     r0, #23
d149 2
a150 2
        TEQ     r0, #26
        TEQNE   r0, #27
d153 1
a153 1
        TEQ     r0, #127
d157 1
a157 1
        TEQ     r2, #&A
d159 1
a159 1
        TEQ     r2, #&1A
d161 1
a161 2

        TEQ     r2, #&22
a170 4
key_open_root
        NOP
        EXIT

a171 1
        ;TODO - will probably want to force confirmation here?
@


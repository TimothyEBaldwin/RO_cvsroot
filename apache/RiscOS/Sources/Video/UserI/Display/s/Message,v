head	4.6;
access;
symbols
	Display-0_42-1:4.5
	Display-0_44:4.6
	Display-0_43:4.6
	Display-0_42:4.5
	Display-0_41:4.5
	Display-0_40:4.3
	Display-0_39:4.2
	RO_5_07:4.2
	Display-0_38:4.2
	Display-0_37:4.2
	Ursula_merge:4.1
	Display-0_36:4.1
	Display-0_34:4.1
	mstphens_UrsulaRiscPCBuild_20Nov98:4.1
	Ursula_RiscPC:4.1.0.8
	rthornb_UrsulaBuild-19Aug1998:4.1
	UrsulaBuild_FinalSoftload:4.1
	rthornb_UrsulaBuild-12Aug1998:4.1
	aglover_UrsulaBuild-05Aug1998:4.1
	rthornb_UrsulaBuild-29Jul1998:4.1
	rthornb_UrsulaBuild-22Jul1998:4.1
	rthornb_UrsulaBuild-15Jul1998:4.1
	rthornb_UrsulaBuild-07Jul1998:4.1
	rthornb_UrsulaBuild-17Jun1998:4.1
	rthornb_UrsulaBuild-03Jun1998:4.1
	rthornb_UrsulaBuild-27May1998:4.1
	rthornb_UrsulaBuild-21May1998:4.1
	rthornb_UrsulaBuild_01May1998:4.1
	Spinner_RCA116:4.1.7.1
	Spinner_B20_2:4.1.7.1
	Spinner_19_3:4.1.7.1
	Spinner_B18:4.1.7.1
	Spinner_B17:4.1.7.1
	Spinner_B15:4.1.7.1
	Spinner_B14:4.1.7.1
	Spinner_B13:4.1.7.1
	Spinner_B12:4.1.7.1
	Spinner_B10:4.1.7.1
	Daytona:4.1.0.6
	Daytona_bp:4.1
	Ursula:4.1.0.4
	Ursula_bp:4.1
	Spinner_B7:4.1.7.1
	RO_3_71:4.1.3.1
	ARTtmp_merge:4.1.7.1
	Spin_3Apr97:4.1.7.1
	RCA_bp:4.1
	ARTtmp:4.1.7.1.0.2
	RCA:4.1.0.2
	Spin_merge:4.1.7.1
	MergeFiles:4.1.3.1
	RO_3_70:4.1.3.1
	NC_1_06:4.1.7.1
	Spinner:4.1.7
	Spin_xx:4.1.5
	NC_xx:4.1.5.1
	RO_3_60:4.1.1.1
	StrongARM:4.1.3
	Black:4.1.1;
locks; strict;
comment	@# @;


4.6
date	2016.05.28.08.26.09;	author rool;	state Exp;
branches;
next	4.5;
commitid	LY36IjFWZTOVZd8z;

4.5
date	2015.01.16.00.55.02;	author jlee;	state Exp;
branches;
next	4.4;
commitid	yM5i84ar30KGvb6y;

4.4
date	2013.12.15.21.52.29;	author jlee;	state Exp;
branches;
next	4.3;
commitid	IbEDXw8q6YTcehhx;

4.3
date	2009.05.13.00.05.03;	author jlee;	state Exp;
branches;
next	4.2;

4.2
date	2001.03.23.17.13.57;	author sbrodie;	state Exp;
branches;
next	4.1;

4.1
date	96.11.05.09.45.43;	author nturton;	state Exp;
branches
	4.1.1.1
	4.1.3.1
	4.1.5.1
	4.1.7.1;
next	;

4.1.1.1
date	96.11.05.09.45.43;	author nturton;	state Exp;
branches;
next	;

4.1.3.1
date	96.11.06.02.45.39;	author nturton;	state Exp;
branches;
next	;

4.1.5.1
date	96.11.21.13.00.01;	author nturton;	state Exp;
branches;
next	;

4.1.7.1
date	96.11.29.21.22.18;	author nturton;	state Exp;
branches;
next	;


desc
@@


4.6
log
@Accept dropped EDID or MDF
Detail:
  Since LoadModeFile will load EDID files too, accept both being dropped on Display Manager.
Admin:
  Submission for the EDID bounty.

Version 0.43. Tagged as 'Display-0_43'
@
text
@; Copyright 1996 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
; > Sources.Message

;---------------------------------------------------------------------------
; Message_Received
; Message_Recorded
;
;       In:     r1 -> message block
;       Out:    r0 corrupted
;
;       Deal with messages which require no ack.
;
Message_Received
Message_Recorded
        Entry   "r1-r6"

        LDR     r0, [r1, #ms_action]

        Debug   msg,"Message",r0

        LDR     lr, =Message_MenuWarning
        TEQ     r0, lr
        BEQ     msg_menuwarning

        LDR     lr, =Message_MenusDeleted
        TEQ     r0, lr
        BEQ     msg_menusdeleted

        LDR     lr, =Message_HelpRequest
        TEQ     r0, lr
        BEQ     msg_helprequest

 [ LoadModeFiles
        TEQ     r0, #Message_DataLoad
        BEQ     msg_dataload

        TEQ     r0, #Message_DataSave
        BEQ     msg_datasave
 ]

        TEQ     r0, #Message_Quit
        EXIT    NE

msg_quit
        Debug   msg,"msg_quit"
        BL      Mod_ShutDown
        SWI     OS_Exit


msg_menuwarning
        Debug   msg,"msg_menuwarning"
        ADD     r1, r1, #20
        LDMIA   r1, {r1-r3}
        Push    "r1-r3"
        SUB     sp, sp, #GVPixelFormat_Size

        BL      Mode_GetCurrent
        MOV     r5, sp
        BLVC    Mode_GetInfo
        BLVC    Mode_TestPalette
        BLVC    Mode_SetModeString

        ADD     sp, sp, #GVPixelFormat_Size
        Pull    "r1-r3"
        SWIVC   XWimp_CreateSubMenu

        EXIT


msg_menusdeleted
        Debug   msg,"msg_menusdeleted"
        LDR     r1, menu_handle
        BL      Menu_RemoveTick
        MOV     r1, #0
        STR     r1, menu_handle
        EXIT


msg_helprequest
        Debug   msg,"msg_helprequest"
        LDR     r2, [r1, #ms_data + b_window]
        LDR     r3, [r1, #ms_data + b_icon]

        CMP     r2, #iconbar_whandle
        MOVEQ   r2, #"B"
        MOVEQ   r3, #0
        BEQ     return_help

        LDR     lr, info_handle
        TEQ     r2, lr
        MOVEQ   r2, #"I"
        MOVEQ   r3, #0
        BEQ     return_help

        LDR     lr, display_handle
        TEQ     r2, lr
        MOVEQ   r2, #"D"
        BEQ     return_help

        LDR     lr, mode_handle
        TEQ     r2, lr
        MOVEQ   r2, #"W"
        BEQ     return_help

        CMP     r3, #0                  ; No help if not on a menu item.
        EXIT    LT

        Push    "r1,r3,lr"              ; Find menu item.
        MOV     r0, #1
        ADD     r1, sp, #4
        SWI     XWimp_GetMenuState
        Pull    "r1,r3,lr"              ; r3 = menu item
        EXIT    VS

        LDR     r0, menu_handle
        ADR     lr, m_iconbarmenu
        TEQ     r0, lr
        MOVEQ   r2, #"M"
        BEQ     return_help

        ADR     lr, m_coloursmenu
        TEQ     r0, lr
        MOVEQ   r2, #"C"
        BEQ     return_help

        LDR     lr, m_resolutionmenu
        TEQ     r0, lr
        MOVEQ   r2, #"R"
        BEQ     return_help

        LDR     lr, m_ratemenu
        TEQ     r0, lr
        EXIT    NE

        MOV     r2, #"F"                ; And drop through to...

return_help
        SUB     sp, sp, #8              ; Get some local stack.

        MOV     r1, sp                  ; Build message token.
        MOV     lr, #"H"
        STRB    lr, [r1], #1
        STRB    r2, [r1], #1
        MOV     r0, r3
        MOV     r2, #user_data_size - msHelp_message
        SWI     XOS_ConvertHex2

        BLVC    MsgTrans_OpenFile       ; Lookup help text.
        MOVVC   r1, sp
        ADRVC   r2, user_data + msHelp_message
        MOVVC   r3, #user_data_size - msHelp_message
        BLVC    MsgTrans_Lookup

        ADD     sp, sp, #8              ; Balance stack.

        LDRVC   r0, =Message_HelpReply
        SUBVC   r1, r2, #msHelp_message
        ADDVC   r3, r3, #msHelp_message + 4
        BICVC   r3, r3, #3              ; r3 = message size (including string terminator)
        STRVC   r3, [r1, #ms_size]
        BLVC    Message_SendAck
        EXIT


 [ LoadModeFiles

msg_dataload
        Debug   msg,"msg_dataload"
        LDR     lr, [r1, #msDataTransfer_filetype]
        LDR     r0, =FileType_Text
        TEQ     r0, lr                  ; Make sure it's a text file.
        LDRNE   r0, =FileType_EDID
        TEQNE   r0, lr                  ; Or an EDID file.
        ADRNE   r0, ErrorBlock_Modes_InvalidFile
        BLNE    MsgTrans_ErrorLookup
        EXIT    VS

        LDR     r6, [r1, #ms_yourref]   ; Save for later.
        Debug   msg," received ref",r6

        LDR     r0, =Message_DataLoadAck
        BL      Message_SendAck

        ADDVC   r1, r1, #msDataTransfer_filename
        BLVC    Mode_LoadFile
        BL      delete_scrap
        EXIT

delete_scrap
        EntryS  "r0"
        LDR     lr, scrap_ref
        TEQ     r6, lr                  ; If loading scrap file then
        MOVEQ   lr, #-1                 ;   clear scrap ref
        STREQ   lr, scrap_ref
        ADREQ   r0, deletescrap         ;   and delete scrap file we used.
        SWIEQ   XOS_CLI
        EXITS


msg_datasave
        Debug   msg,"msg_datasave"
        LDR     lr, [r1, #msDataTransfer_filetype]
        LDR     r0, =FileType_Text
        TEQ     r0, lr                  ; Make sure it's a text file.
        EXIT    NE

        ADR     r0, wimpscrapname       ; Check that Wimp$Scrap has been set.
        MOV     r2, #-1
        MOV     r3, #0
        MOV     r4, #VarType_Expanded
        SWI     XOS_ReadVarVal          ; Returns r2=0 if doesn't exist.
        CMP     r2, #0
        ADREQ   r0, ErrorBlock_Modes_NoScrap
        BLEQ    MsgTrans_ErrorLookup
        EXIT    VS

        MOV     lr, #-1
        STR     lr, [r1, #msDataTransfer_filesize]
        MOV     lr, #(msDataTransfer_filename + ?wimpscrapname2 + 3):AND::NOT:3
        STR     lr, [r1, #ms_size]

        ADR     r0, wimpscrapname2
        ADD     r1, r1, #msDataTransfer_filename
        BL      Mod_CopyString          ; r2 = length = -1 from above.

        LDR     r0, =Message_DataSaveAck
        ADR     r1, user_data
        BL      Message_SendAck

        LDRVC   lr, [r1, #ms_myref]     ; Note that we're using Wimp$Scrap.
        STRVC   lr, scrap_ref
        Debug   msg," sent ref",lr
        EXIT

wimpscrapname   DCB     "Wimp$$Scrap",0
wimpscrapname2  DCB     "<Wimp$$Scrap>",0
deletescrap     DCB     "%Delete <Wimp$$Scrap>",0
                ALIGN

        MakeErrorBlock  Modes_InvalidFile
        MakeErrorBlock  Modes_NoScrap


;---------------------------------------------------------------------------
; Message_SendAck
;
;       In:     r0 = message to send
;               r1 -> message block to send
;       Out:    r0 corrupted
;
;       The references in the message block are updated and the message
;       is then sent to the appropriate task.
;
Message_SendAck
        Entry   "r1,r2"

        LDR     r2, [r1, #ms_taskhandle]
        LDR     lr, [r1, #ms_myref]
        STR     lr, [r1, #ms_yourref]
        STR     r0, [r1, #ms_action]

        MOV     r0, #User_Message
        SWI     XWimp_SendMessage

        EXIT
 ]

        LTORG

        END
@


4.5
log
@Escape some dollars
Detail:
  s/Message, s/Module - Escape some dollars contained in strings to avoid warnings from objasm
Admin:
  Resulting binary unchanged


Version 0.41. Retagged as 'Display-0_41'
@
text
@d185 2
@


4.4
log
@Add support for new screen modes: 4K/64K colour modes and RGB/alpha variants
Detail:
  Resources/UK/Messages, Resources/Germany/Messages - Add entries M19 and M20 for 4K & 64K colour modes
  s/Front - Adjust workspace for the two new menu entries. Add definition for the new mode provider block format (referred to as mode descriptor blocks in the sources)
  s/GetAll - Get Hdr:GraphicsV as the new mode provider block uses the GVPixelFormat structure. Strip out NewShading option; now hard-coded to be true. Add new Allow24bpp option for control over whether we allow 24bpp modes to be used.
  s/Menu, s/Message, s/Mode - Lots of changes to add support for the new menu entries and handling of the new mode provider block format/pixel formats. Strip out old mode specifier -> string conversion code and instead just rely on OS_ScreenMode 14.
Admin:
  Tested in Tungsten, IOMD, OMAP3 & BCM2835 ROMs
  Requires HdrSrc-2_38 and Kernel-5_35-4_79_2_203
  New German messages are in need of translation


Version 0.41. Tagged as 'Display-0_41'
@
text
@d246 3
a248 3
wimpscrapname   DCB     "Wimp$Scrap",0
wimpscrapname2  DCB     "<Wimp$Scrap>",0
deletescrap     DCB     "%Delete <Wimp$Scrap>",0
@


4.3
log
@Fix previously undiscovered 32bit incompatability in Display Manager
Detail:
  s/Message - fix delete_scrap to use EntryS/EXITS macros to preserve PSR instead of Pull "r0,pc",,^
Admin:
  Tested on rev C2 beagleboard.


Version 0.40. Tagged as 'Display-0_40'
@
text
@d68 1
d71 1
d76 1
@


4.2
log
@  Updated build structure to use the shared AAsmModule makefile.
  Updated to build using objasm instead of aasm.
  Sources changed to be objasm-compatible.
Admin:
  Requires Library 0.72 or later.
  Requires BuildSys 3.09 or later.
  Requires Env 0.65 or later.

Version 0.37. Tagged as 'Display-0_37'
@
text
@d198 1
a198 1
        Push    "r0,lr"
d205 1
a205 1
        Pull    "r0,pc",,^
@


4.1
log
@Initial revision
@
text
@d28 1
a28 1
        ENTRY   "r1-r6"
d263 1
a263 1
        ENTRY   "r1,r2"
@


4.1.7.1
log
@NCOS 1.06 Imported from Zip drive
@
text
@@


4.1.5.1
log
@Import from SrcFiler
@
text
@@


4.1.3.1
log
@Import from cleaned 370 CD
@
text
@@


4.1.1.1
log
@Import from cleaned 360 CD
@
text
@@

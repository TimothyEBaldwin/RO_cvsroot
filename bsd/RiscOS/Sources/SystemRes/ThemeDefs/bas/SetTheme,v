head	1.9;
access;
symbols
	ThemeDefs-0_11:1.9
	ThemeDefs-0_10:1.8
	ThemeDefs-0_09:1.8
	ThemeDefs-0_08:1.7
	ThemeDefs-0_07:1.7
	ThemeDefs-0_06:1.6
	ThemeDefs-0_05:1.5
	ThemeDefs-0_04:1.4
	ThemeDefs-0_03:1.3
	ThemeDefs-0_02:1.2
	ThemeDefs-0_01:1.1.1.1
	ThemeDefs-0_00:1.1.1.1
	vendor:1.1.1;
locks; strict;
comment	@# @;


1.9
date	2013.08.25.11.13.19;	author rsprowson;	state Exp;
branches;
next	1.8;
commitid	3gzb2HUNHGN9iP2x;

1.8
date	2013.05.06.19.13.44;	author rsprowson;	state Exp;
branches;
next	1.7;
commitid	qXxExT6X8lzawBOw;

1.7
date	2012.09.18.08.03.53;	author rsprowson;	state Exp;
branches;
next	1.6;
commitid	71EeKGRe9rxKcZkw;

1.6
date	2012.09.05.16.39.45;	author rsprowson;	state Exp;
branches;
next	1.5;
commitid	1mcEhxWzKv3Dtmjw;

1.5
date	2012.08.22.21.18.00;	author jlee;	state Exp;
branches;
next	1.4;
commitid	OIBekuuLjYYZsAhw;

1.4
date	2012.08.22.17.40.03;	author rsprowson;	state Exp;
branches;
next	1.3;
commitid	xLlaSbvObBadgzhw;

1.3
date	2012.08.22.13.42.53;	author rsprowson;	state Exp;
branches;
next	1.2;
commitid	oybD62wodcXQWxhw;

1.2
date	2012.08.11.20.38.18;	author rsprowson;	state Exp;
branches;
next	1.1;
commitid	7IuvnbwVV0KhBagw;

1.1
date	2012.08.09.20.30.57;	author rsprowson;	state Exp;
branches
	1.1.1.1;
next	;
commitid	2JWc0IgoUCEHCUfw;

1.1.1.1
date	2012.08.09.20.30.57;	author rsprowson;	state Exp;
branches;
next	;
commitid	2JWc0IgoUCEHCUfw;


desc
@@


1.9
log
@Add support for optional theming of SCSIFiler and SDFSFiler
Table extended to look for alternate theme sprites for these filers, using the sprites from Chris Wraight.
New flag option to ensure the target already exists before adding to ResourceFS, so those versions of RISC OS that don't have certain modules don't needlessly load extra resources that aren't going to be used.
Tested on BBxM with RISC OS 5.20 and an Ursula theme selected.

Version 0.11. Tagged as 'ThemeDefs-0_11'
@
text
@REM>!ThemeDefs.!RunImage
REM
REM Copyright (c) 2012, RISC OS Open Ltd
REM All rights reserved.
REM 
REM Redistribution and use in source and binary forms, with or without
REM modification, are permitted provided that the following conditions are met: 
REM     * Redistributions of source code must retain the above copyright
REM       notice, this list of conditions and the following disclaimer.
REM     * Redistributions in binary form must reproduce the above copyright
REM       notice, this list of conditions and the following disclaimer in the
REM       documentation and/or other materials provided with the distribution.
REM     * Neither the name of RISC OS Open Ltd nor the names of its contributors
REM       may be used to endorse or promote products derived from this software
REM       without specific prior written permission.
REM 
REM THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
REM AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
REM IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
REM ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
REM LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
REM CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
REM SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
REM INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
REM CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
REM ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
REM POSSIBILITY OF SUCH DAMAGE.
REM 
:
ONERRORPRINT"ThemeDefs: "+REPORT$+"/"+STR$ERL:END:REM Hopefully never used
:
resfsnodesize%=24 :REM Per the PRM
resfsreqd%=FALSE  :REM Assume not
resfssize%=4      :REM A word for the end node
areakey%=&1234ABCD:REM Additional uniqueness beyond the name
areaflags%=1<<7   :REM Not resizeable
xsquash_compress%=&62700:REM Name wouldn't lookup if absent
xos_dynamicarea%=&20066 :REM Name wouldn't lookup if absent
xos_leaveos%=&2007C     :REM Name wouldn't lookup with CallASWI
:
REM Inspect sprite precedence CMOS flag
SYS"XOS_Byte",161,&8C TO,,cmos%:romprotect%=(cmos% AND32)<>0
REM Require squashed banners from Desktop 2.71+
SYS"XOS_CLI","RMEnsure Desktop 2.71" TO;flags%
squashbanner%=(flags% AND1)=0
REM Need to rename the switcher icon prior to TaskManager 1.19
SYS"XOS_CLI","RMEnsure TaskManager 1.19" TO;flags%
switchername%=(flags% AND1)=1
switcherreqd%=FALSE
REM Need dynamic areas
SYSxos_dynamicarea%,3,-1 TO;flags%
dynamic%=(flags% AND1)=0
:
REM See which theme resources are installed with this theme
found%=FALSE
RESTORE
REPEAT
  READdst$,src$,opt%
  SYS"XOS_File",23,src$ TOobject%,,,,size%,,type%
  IF(object%=1)AND(type%=&FF9) THEN
    IF(opt% AND32)<>0 THEN SYS"XOS_File",23,"Resources:$."+dst$ TOobject%
    IF(object%=1) THEN
      found%=TRUE
      IFFNintoresfs(romprotect%,opt%) THEN
        REM That goes in ResourceFS area
        resfsreqd%=TRUE
        resfssize%=resfssize%+FNroundtoword(size%)+FNroundtoword(1+LENdst$)+resfsnodesize%
      ENDIF
    ENDIF
  ENDIF
UNTILopt%=-1
:
REM Anything for this theme?
IFNOTfound% THENEND
:
REM Get area name
SYS"MessageTrans_FileInfo",,"<ThemeDefs$Dir>.Messages" TO,,msgsize%;flags%
IF(flags% AND1)=1 THENEND
DIMmsgdata% msgsize%
DIMmsginfo% 16
SYS"XMessageTrans_OpenFile",msginfo%,"<ThemeDefs$Dir>.Messages",msgdata%
SYS"XMessageTrans_Lookup",msginfo%,"Area",STRING$(32,"@@"),32 TO,,areaname$
SYS"XMessageTrans_Lookup",msginfo%,"ModHelp",STRING$(32,"@@"),32 TO,,modhelp$
SYS"XMessageTrans_Lookup",msginfo%,"_Version",STRING$(32,"@@"),32 TO,,version$
SYS"XMessageTrans_CloseFile",msginfo%
:
REM See if the resources already exist, implying we're being run for a second time
REM We can't delete the old area because that'd leave ResourceFS pointing at 
REM unmapped memory, and we can't merge partial sets because the ROM ones are gone
IFdynamic% THEN
  areanum%=-1
  REPEAT
    SYS"XOS_DynamicArea",3,areanum% TO,areanum%;flags%
    IFareanum%<>-1 THEN
      SYS"XOS_DynamicArea",2,areanum% TO,,,,,,h%,w%,name$
      IF(w%=areakey%)AND(name$=areaname$)AND(h%=0) THEN
        REM Workspace is the magic word, name matches, no handler, looks like ours
        REM so stop now rather than end up with two copies. 
        END
      ENDIF
    ENDIF
  UNTILareanum%=-1
ELSE
  SYS"XOS_Module",18,"ThemeDefs" TO;flags%
  IF(flags% AND1)=0 THEN
    REM Module is in the chain, so stop now rather than end up with two copies.
    END
  ENDIF
ENDIF
:
IFresfsreqd% THEN
  IFdynamic% THEN
    REM Make a dynamic area to load stuff into it
    SYS"XOS_DynamicArea",0,-1,resfssize%,-1,areaflags%,resfssize%,0,areakey%,areaname$ TO,areanum%,,areabase%;flags%
    IF(flags% AND1)=1 THENEND
    resfsnext%=areabase%
  ELSE
    REM Make a simple module to load stuff into
    PROCmakemodule(modhelp$,version$,resfssize%,modbase%,resfsnext%)
  ENDIF
ELSE
  DIMresfsnext% 4:REM Enough to hold a dummy end node
ENDIF
RESTORE
REPEAT
  READdst$,src$,opt%
  IF(opt% AND32)<>0 THEN SYS"XOS_File",23,"Resources:$."+dst$ TOobject% ELSEobject%=1
  IF(object%=1) THEN
    IFFNintoresfs(romprotect%,opt%) THEN
      resfsnext%=FNaddtoarea(dst$,src$,resfsnext%,opt%)
    ELSE
      PROCaddtopool(src$,opt%)
    ENDIF
    IF(opt% AND3)=1 THENswitcherreqd%=TRUE:REM Got icons to load
  ENDIF
UNTILopt%=-1
!resfsnext%=0:REM Terminate the ResourceFS list
IFswitcherreqd% THEN
  IFswitchername% THEN
    PROCpatchswitcher
  ENDIF
ENDIF
IFresfsreqd% THEN
  IFdynamic% THEN
    SYS"ResourceFS_RegisterFiles",areabase%
  ELSE
    SYS"XOS_Module",10,modbase%+4
  ENDIF
ENDIF
END
:
DEFPROCaddtopool(src$,opt%)
LOCALobject%
IFsrc$="" THENENDPROC
SYS"XOS_File",23,src$ TOobject%:IFobject%<>1 THENENDPROC
REM The flags denote which are the base names, the Wimp will 
REM select which resolution it thinks is best. Note, the actual
REM loading is deferred until the desktop is entered when 
REM the WimpMode is defined
IF(opt% AND3)=1 THENSYS"XOS_CLI","SET ThemeDefs$ToBeIcons "+src$
IF(opt% AND6)=4 THENSYS"XOS_CLI","SET ThemeDefs$ToBeTools "+src$
ENDPROC
:
DEFFNaddtoarea(dst$,src$,ptr%,opt%)
LOCALload%,exec%,size%,nameend%,data%,squash%
squash%=((opt% AND8)=8) ANDsquashbanner%
nameend%=20+FNroundtoword(1+LENdst$)
data%=ptr%+nameend%+4
IFsquash% THEN
  LOCALwork%,buffer%,flags%,end%
  SYS"XOS_File",23,src$ TO,,,,size%
  SYSxsquash_compress%,8,-1 TOwork%;flags%
  IF(flags% AND1)=1 THEN=ptr%:REM Infer no Squash module
  SYS"XOS_Module",6,,,work%+size% TO,,buffer%;flags%
  IF(flags% AND1)=1 THEN=ptr%:REM Not enough RMA
  work%=buffer%+size%:REM File buffer and workspace consolidated in one claim
  SYS"XOS_File",16,src$,buffer%,0 TO,,load%,exec%;flags%
  IF(flags% AND1)=0 THEN
    REM Do the compress operation and add the 20 byte header
    SYSxsquash_compress%,0,work%,buffer%,size%,data%+20,size%-20 TOflags%,,,,end%
    $data%="SQSH"
    data%!4=size%
    data%!8=load%
    data%!12=exec%
    data%!16=0
    size%=end%-data%:REM The compressed size including the header
    load%=(load% AND&FFF000FF)OR&FCA00:REM Adjust filetype to Squash
  ELSE
    flags%=-1:REM Failed to load
  ENDIF
  SYS"XOS_Module",7,,buffer%
  IFflags%<>0 THEN=ptr%:REM Failed to compress
ELSE
  SYS"XOS_File",16,src$,data%,0 TO,,load%,exec%,size%;flags%
  IF(flags% AND1)=1 THEN=ptr%:REM Failed to load
ENDIF
!(ptr%+0)=FNroundtoword(1+LENdst$)+FNroundtoword(size%)+resfsnodesize%
!(ptr%+4)=load%
!(ptr%+8)=exec%
!(ptr%+12)=size%
!(ptr%+16)=%10001:REM Public/owner read
$(ptr%+20)=dst$
?(ptr%+20+LENdst$)=0:REM Null terminate
!(ptr%+nameend%)=size%+4
=data%+FNroundtoword(size%)
:
DEFFNroundtoword(V%)
=(V%+3)ANDNOT3
:
DEFFNintoresfs(romprotect%,opt%)
IFromprotect% OR((opt% AND16)=16) THEN=TRUE
=FALSE
:
DEFPROCmakemodule(modhelp$,version$,resfssize%,RETURN modbase%,RETURN resfsnext%)
LOCALflags%,pass%,modcode%
modcode%=256
SYS"XOS_Module",6,,,resfssize%+modcode% TO,,modbase%;flags%
IF(flags% AND1)=1 THENEND
version$=LEFT$(version$,INSTR(version$,".")+2)
modhelp$=modhelp$+STRING$(2-(LENmodhelp$ DIV8),CHR$9):REM Tabs to column 16
?modbase%=3:SYS"OS_Word",14,modbase%:REM Module is dated from now
SYS"OS_ConvertDateAndTime",modbase%,STRING$(32,"@@"),32," (%DY %M3 %CE%YR)" TOdate$
FORpass%=0TO2STEP2
P%=modbase%
[OPTpass%
 EQUD    resfssize%+modcode% \Size for OS_Module
 :
.header
 EQUD    0                   \No language entry
 EQUD    init-header
 EQUD    final-header
 EQUD    0                   \No service calls
 EQUD    title-header
 EQUD    help-header
 EQUD    0                   \No commands
.title
 EQUS    "ThemeDefs"+CHR$0
.help
 EQUS    modhelp$+version$+date$+CHR$0
 ALIGN
.init
 MOV     r6,r14
 ADR     r0,resfsnext%
 SWI     "XResourceFS_RegisterFiles"
 MOV     pc,r6
.final
 MOV     r6,r14
 ADR     r0,resfsnext%
 SWI     "XResourceFS_DeregisterFiles"
 CMP     pc,#0               \Don't refuse to die on error
 MOV     pc,r6
.resfsnext%
]
NEXT
ENDPROC
:
DEFPROCpatchswitcher
LOCALflags%,base%,pass%,patch%
SYS"XOS_CLI","RMFaster TaskManager" TO;flags%
IF(flags% AND1)=1 THENENDPROC
REM Find out where it got placed
SYS"XOS_Module",18,"TaskManager%Base" TO,,,base%
DIMpatch% 256
FORpass%=0TO2STEP2
P%=patch%
[OPTpass%
.setup
 ADR     r1,search
 LDMIA   r1,{r1-r3}          \Known word aligned case sensitive
 MOV     r6,r14
 LDR     r0,origin
 LDR     r4,[r0,#-4]         \Size from module chain
 ADD     r4,r4,r0
.seek
 LDR     r5,[r0],#4
 TEQ     r5,r1
 LDREQ   r5,[r0,#0]
 TEQEQ   r5,r2
 LDREQ   r5,[r0,#4]
 TEQEQ   r5,r3
 BEQ     hit
 CMP     r0,r4               \End?
 BCC     seek
 MOV     r0,#0               \No match
 B       done
.hit
 SUB     r0,r0,#4
 ADR     r1,replace
 LDMIA   r1,{r1-r3}
 SWI     "XOS_EnterOS"
 STMIA   r0,{r1-r3}
 SWI     xos_leaveos%
 MVN     r0,#0               \Matched and patched
.done
 CMP     pc,#0
 EQUD    &E328F202           \MSR CPSR_f,#C_bit
 MOV     pc,r6
.origin
 EQUD    base%  
.search
 EQUS    "switcher"+CHR$0+CHR$0+CHR$0+CHR$0
.replace
 EQUS    "taskmanager"+CHR$0
]
NEXT
CALLsetup
REM Make sure one is there on entering the desktop
SYS"XOS_CLI","IconSprites ThemeDefs:Switcher"
ENDPROC
:
REM Flag 1=icons
REM      2=alternate resolutions of base name
REM      4=tools
REM      8=desktop banner
REM      16=must go in ResourceFS
REM      32=check target already exists in ResourceFS
DATA"Resources.Wimp.Sprites",       "<ThemeDefs$Dir>.Themes.<Wimp$IconTheme>Sprites"  ,1
DATA"Resources.Wimp.Sprites22",     "<ThemeDefs$Dir>.Themes.<Wimp$IconTheme>Sprites22",3
DATA"Resources.Wimp.Sprites11",     "<ThemeDefs$Dir>.Themes.<Wimp$IconTheme>Sprites11",3
DATA"Resources.Wimp.Tools",         "<ThemeDefs$Dir>.Themes.<Wimp$IconTheme>Tools"    ,4
DATA"Resources.Desktop.Sprites",    "<ThemeDefs$Dir>.Themes.<Wimp$IconTheme>Banner"   ,24
DATA"Resources.SCSIFiler.Sprites",  "<ThemeDefs$Dir>.Themes.<Wimp$IconTheme>SCSIFiler",48
DATA"Resources.SDFSFiler.Sprites",  "<ThemeDefs$Dir>.Themes.<Wimp$IconTheme>SDFSFiler",48
DATA"Resources.Paint.Sprites",      "<ThemeDefs$Dir>.Themes.<Wimp$IconTheme>Paint"    ,16
DATA"Resources.Paint.Sprites22",    "<ThemeDefs$Dir>.Themes.<Wimp$IconTheme>Paint22"  ,18
DATA"Resources.Paint.Sprites11",    "<ThemeDefs$Dir>.Themes.<Wimp$IconTheme>Paint11"  ,18
DATA"Resources.Draw.Sprites",       "<ThemeDefs$Dir>.Themes.<Wimp$IconTheme>Draw"     ,16
DATA"Resources.Draw.Sprites22",     "<ThemeDefs$Dir>.Themes.<Wimp$IconTheme>Draw22"   ,18
DATA"Resources.Draw.Sprites11",     "<ThemeDefs$Dir>.Themes.<Wimp$IconTheme>Draw11"   ,18
DATA"","",-1
@


1.8
log
@Add fixup step for task manager sprite pre RISC OS 5
We want two distinct sprites, 'switcher' for unknown error boxes and the OS info box, and 'taskmanager' for the tote in the bottom right of the iconbar.
If we find an older version of TaskManager where they're both 'switcher' the theme loader patches it to make them distinct.
Them, the same set of sprite resources works on everything.
Tested on RISC OS 3.50, 3.60, 3.70, 3.71, 4.02.

Version 0.09. Tagged as 'ThemeDefs-0_09'
@
text
@d61 8
a68 5
    found%=TRUE
    IFFNintoresfs(romprotect%,opt%) THEN
      REM That goes in ResourceFS area
      resfsreqd%=TRUE
      resfssize%=resfssize%+FNroundtoword(size%)+FNroundtoword(1+LENdst$)+resfsnodesize%
d127 8
a134 4
  IFFNintoresfs(romprotect%,opt%) THEN
    resfsnext%=FNaddtoarea(dst$,src$,resfsnext%,opt%)
  ELSE
    PROCaddtopool(src$,opt%)
a135 1
  IF(opt% AND3)=1 THENswitcherreqd%=TRUE:REM Got icons to load
d181 1
a181 1
    data%!0=&48535153:REM "SQSH"
d316 1
d322 2
@


1.7
log
@Fix 'sprite not found' from CDFSFiler pre RISC OS 3.80
The swap out of the CDFSFiler sprites was previously required because entering the desktop runs Desktop_CDFSFiler which then *ICONSPRITESd its private sprites if 'protect built in icons' option was off.
Now the disc pool is loaded after *DESKTOP that left CDFSFiler with no sprites prior to RISC OS 3.80.
The logic was rendered obsolete by the 'IconChange' tool, so can be removed.

Version 0.07. Tagged as 'ThemeDefs-0_07'
@
text
@d39 1
d46 4
d90 1
a90 1
    SYSxos_dynamicarea%,3,areanum% TO,areanum%;flags%
d92 1
a92 1
      SYSxos_dynamicarea%,2,areanum% TO,,,,,,h%,w%,name$
d111 1
a111 1
    SYSxos_dynamicarea%,0,-1,resfssize%,-1,areaflags%,resfssize%,0,areakey%,areaname$ TO,areanum%,,areabase%;flags%
d125 1
a125 1
    resfsnext%=FNaddtoarea(dst$,src$,resfsnext%,((opt% AND8)=8) ANDsquashbanner%)
d129 1
d132 5
d158 3
a160 2
DEFFNaddtoarea(dst$,src$,ptr%,squash%)
LOCALload%,exec%,size%,nameend%,data%
d251 54
@


1.6
log
@Import of 180x180 90x90 90x45 resolution icons
Special thanks to Richard Hallas for providing these.
Also
* Makefile - swapped hard spaces for soft, install extra sprites
* Remove redundant termination step fixed in ThemeDefs-0_05
* Updated docs to not refer to Tools22 Tools11

Version 0.06. Tagged as 'ThemeDefs-0_06'
@
text
@a47 3
REM Damage the sprites in CDFSFiler so the Wimp pool is used
SYS"XOS_CLI","RMEnsure CDFSFiler 2.42" TO;flags%
swapcdfs%=(flags% AND1)=1:loadicons%=FALSE
d57 1
a57 5
    IF(opt% AND1)=1 THEN
      REM When loading icons, may need to nobble the CDFSFiler sprites later
      loadicons%=TRUE
    ENDIF      
    IFFNintoresfs(romprotect%,opt%,loadicons%,swapcdfs%) THEN
d119 1
a119 1
  IFFNintoresfs(romprotect%,opt%,loadicons%,swapcdfs%) THEN
d192 1
a192 5
DEFFNintoresfs(romprotect%,opt%,loadicons%,swapcdfs%)
IF((opt% AND32)=32) THEN
  REM It's the CDFSFiler sprites, nobble or not?
  IFloadicons% ANDswapcdfs% THEN=TRUE ELSE=FALSE
ENDIF
a243 1
REM      32=for CDFS but only if icons are loaded (entry must be after all flag 1's)
a254 2
DATA"Resources.CDFSFiler.Sprites",  "<ThemeDefs$Dir>.!Sprites"                        ,48
DATA"Resources.CDFSFiler.Sprites22","<ThemeDefs$Dir>.!Sprites"                        ,50
@


1.5
log
@Ensure resource FS area is terminated
Detail:
  bas/SetTheme - Despite being careful to allocate enough space for the terminating word of the resource FS area, no termination was taking place, most likely leading to a crash in ResourceFS after adding the area
Admin:
  Tested on Raspberry Pi
  Seems to fix a crash seen in ResourceFS when ThemeDefs is running from within a fresh boot sequence


Version 0.05. Tagged as 'ThemeDefs-0_05'
@
text
@d3 2
a4 2
REM Copyright (c) 2012, RISC OS Open Ltd
REM All rights reserved.
d132 1
a132 1
!resfsnext%=0
a155 1
IFsrc$="" THEN!ptr%=0:=ptr%+4:REM Terminate the ResourceFS list
@


1.4
log
@Different scheme to defer sprite loading
When run within the Desktop file, this app is too late to affect the banner and may also have to mess about re-iconspritesing applications from later in the boot sequence.
So, run !ThemeDefs earlier, and push the *ICON/TOOLSPRITES commands into system variables which are caught in the desktop boot file.

Version 0.04. Tagged as 'ThemeDefs-0_04'
@
text
@d132 1
@


1.3
log
@Get flags for RAM loaded resources right
When the 'protect ROM sprites' option is not in use the resources are loaded with *ICONSPRITES/*TOOLSPRITES.
However, the wrong flag bits were being used to decide which to use and ended up doing the operation 3 times over for each resolution: resulting in force loading Sprites11 (the last in the DATA statements) when not in that mode, resulting in a tiny mouse pointer and lots of badly aliased icons.
Removed the search for Tools22/Tools11 (the tools encode the other resolutions inside the sprite file, not by sprite file name).
Added possibility to have EX0 EY0 paint and draw tool palettes.
Removed the manual check for ThemeSetup obey file, the boot sequence can do this for us.
Docs updated.

Version 0.03. Tagged as 'ThemeDefs-0_03'
@
text
@d146 5
a150 3
REM select which resolution it thinks is best
IF(opt% AND3)=1 THENSYS"XOS_CLI","ICONSPRITES "+src$
IF(opt% AND6)=4 THENSYS"XOS_CLI","TOOLSPRITES "+src$
@


1.2
log
@Add ability to skip loading with ThemeDefs$Disabled
Documentation and comments updated

Version 0.02. Tagged as 'ThemeDefs-0_02'
@
text
@d147 2
a148 2
IF(opt% AND1)=1 THENSYS"XOS_CLI","ICONSPRITES "+src$
IF(opt% AND4)=4 THENSYS"XOS_CLI","TOOLSPRITES "+src$
d253 1
a253 1
REM      32=for CDFS but only if icons are loaded (entry must after all flag 1's)
a257 2
DATA"Resources.Wimp.Tools22",       "<ThemeDefs$Dir>.Themes.<Wimp$IconTheme>Tools22"  ,6
DATA"Resources.Wimp.Tools11",       "<ThemeDefs$Dir>.Themes.<Wimp$IconTheme>Tools11"  ,6
d261 1
d264 1
@


1.1
log
@Initial revision
@
text
@d253 1
a253 1
REM      32=for CDFS but only if icons are loaded (entry must later than flag 1's)
@


1.1.1.1
log
@Import of ThemeDefs
@
text
@@
